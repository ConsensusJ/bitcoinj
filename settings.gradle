import org.gradle.util.GradleVersion
import org.gradle.api.GradleScriptException

// Minimum Gradle version for build
def minGradleVersion = GradleVersion.version("5.6")

rootProject.name = 'bitcoinj-parent'

if (GradleVersion.current().compareTo(minGradleVersion) < 0) {
    throw new GradleScriptException("bitcoinj build requires Gradle ${minGradleVersion} or later", null)
}

include 'core'
project(':core').name = 'bitcoinj-core'

include 'tools'
project(':tools').name = 'bitcoinj-tools'

include 'examples'
project(':examples').name = 'bitcoinj-examples'

if (JavaVersion.current().isJava11Compatible()) {
    System.err.println "Including wallettemplate because Java ${JavaVersion.current()}"
    include 'wallettemplate'
    project(':wallettemplate').name = 'bitcoinj-wallettemplate'
} else {
    System.err.println "Skipping wallettemplate, requires Java 11+, currently running: Java ${JavaVersion.current()}"
}
