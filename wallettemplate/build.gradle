plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'org.beryx.jlink' version '2.10.2'
    id 'org.javamodularity.moduleplugin'    version "1.5.0"
}

repositories {
    maven { url 'https://dl.bintray.com/consensusj/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':bitcoinj-core')

    implementation 'org.consensusj.supernautfx:supernautfx:0.0.2'
    implementation "io.micronaut:micronaut-inject-java:1.1.0"

    implementation 'com.google.guava:guava:27.1-android'
    implementation 'de.jensd:fontawesomefx:8.0.0'
    //implementation 'com.google.zxing:core:3.3.3'
    implementation "com.github.msgilligan.zxing:core:msgilligan-modular-javase-SNAPSHOT"

    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
    implementation 'org.slf4j:slf4j-jdk14:1.8.0-beta4'
    
    implementation "javax.inject:javax.inject:1"
    
    annotationProcessor "io.micronaut:micronaut-inject-java:1.1.0"
}

version = '0.16-SNAPSHOT'

javafx {
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml' ]
}

patchModules.config = [
        "java.annotation=jsr305-3.0.2.jar"
]

sourceCompatibility = 11
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

mainClassName = "$moduleName/wallettemplate.WalletTemplateApp"

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
    // archives javadocJar
}

jlink {
    addExtraDependencies("javafx")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'WalletTemplate'
        jvmArgs = []
    }
    mergedModule {
        requires 'java.logging'
        requires 'java.desktop'

        requires 'javafx.graphics'
        requires 'javafx.controls'

        requires 'org.slf4j'
    }
    jpackage {
        // See https://badass-jlink-plugin.beryx.org/releases/latest/#_jpackage for
        // where the plugin's jpackage task finds the path to the jpackage tool by default
        // (Currently you need to manually install a OpenJDK 13 Early Access Build of jpackage)
        skipInstaller = false
    }
}
