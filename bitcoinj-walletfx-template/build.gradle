plugins {
    id 'java'
    id 'maven'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin'           version '0.0.8'
    id 'org.beryx.jlink'                    version '2.16.0'
    id 'org.javamodularity.moduleplugin'    version "1.5.0"
    id "de.undercouch.download"             version "4.0.0"
}

repositories {
    maven { url 'https://dl.bintray.com/consensusj/maven' }
}

dependencies {
    implementation(project(':bitcoinj-walletfx')) {
        exclude group: 'org.openjfx'
    }
    implementation project(':bitcoinj-core')

    implementation('org.consensusj.supernaut:supernautfx:0.0.4') {
        exclude group: 'org.openjfx'
    }
    implementation "io.micronaut:micronaut-inject-java:1.2.0"

    implementation 'com.google.guava:guava:27.1-android'
    implementation 'de.jensd:fontawesomefx:8.0.0'
    implementation 'com.google.zxing:core:3.4.0'

    implementation 'org.slf4j:slf4j-api:1.7.28'
    implementation 'org.slf4j:slf4j-jdk14:1.7.28'

    implementation "javax.inject:javax.inject:1"

    annotationProcessor "io.micronaut:micronaut-inject-java:1.2.0"
}

version = '0.16-SNAPSHOT'

javafx {
    version = 13
    modules = [ 'javafx.controls', 'javafx.graphics', 'javafx.fxml' ]
}

patchModules.config = [
        "java.annotation=jsr305-3.0.2.jar"
]

sourceCompatibility = 11
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

mainClassName = "$moduleName/wallettemplate.WalletTemplateApp"

def appName = 'WalletTemplate'

ext.os = org.gradle.internal.os.OperatingSystem.current()

jlink {
    addExtraDependencies("javafx")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = appName
        jvmArgs = []
    }
    mergedModule {
        requires 'java.logging'
        requires 'javafx.graphics'
        requires 'javafx.controls'
        
        forceMerge 'org.slf4j'
    }
    jpackage {
        // See https://badass-jlink-plugin.beryx.org/releases/latest/#_jpackage for
        // where the plugin's jpackage task finds the path to the jpackage tool by default
        if (Boolean.getBoolean('download.jpackage')) {
            jpackageHome = downloadJPackage()
        }
        skipInstaller = false
        def appVersionForJpackage = version

        // rpmbuild doesn't like snapshot versions, temporary workaround
        if (os.linux) {
            appVersionForJpackage = '0.0.1'
        }

        imageOptions = ["--app-version", appVersionForJpackage]
        installerOptions = ["--app-version", appVersionForJpackage]
    }
}

// #### The code below is needed only if you use the downloadJPackage() method to install the jpackage tool ####
// Code copied from build.gradle in https://github.com/beryx/fxgl-sliding-puzzle/

/** @return  [url, extension, directory] */
String[] getJPackageCoordinates() {
    def jpackageBaseUrl = 'https://s3-us-west-2.amazonaws.com/static.msgilligan.com/jpackage'
    def jpackageVersionString = 'openjdk-14-jpackage%2B1-49'

    if(os.macOsX) return [
            "${jpackageBaseUrl}/${jpackageVersionString}_osx-x64_bin.tar.gz",
            'tar.gz',
            'jdk-14.jdk/Contents/Home'
    ]
    if(os.windows) return [
            "${jpackageBaseUrl}/${jpackageVersionString}_windows-x64_bin.zip",
            'zip',
            'jdk-14'
    ]
    return [
            "${jpackageBaseUrl}/${jpackageVersionString}_linux-x64_bin.tar.gz",
            'tar.gz',
            'jdk-14'
    ]
}

String downloadJPackage() {
    def (url, extension, directory) = getJPackageCoordinates()
    def downloadDir = "$buildDir/download"
    tasks.jpackageImage.doFirst {
        def execExt = os.windows ? '.exe' : ''
        if(!file("$downloadDir/$directory/bin/jpackage$execExt").file) {
            def jdkArchivePath = "$downloadDir/jdk-jpackage.$extension"
            download {
                src url
                dest jdkArchivePath
                overwrite false
            }
            copy {
                from ((extension == 'tar.gz') ? tarTree(resources.gzip(jdkArchivePath)) : zipTree(jdkArchivePath))
                into downloadDir
            }
        }
    }
    return "$downloadDir/$directory"
}